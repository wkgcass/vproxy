import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'application'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
}

group 'vproxy'
version loadVersion()

mainClassName = 'vproxy.app.app.Main'

jar {
    archiveName 'vproxy.jar'
    manifest {
        attributes 'Main-Class': mainClassName
    }
    exclude 'module-info.class'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    sourceCompatibility = 11.0
    targetCompatibility = 11.0
    compileKotlin {
        kotlinOptions {
            jvmTarget = '11'
            freeCompilerArgs += '-Xjvm-default=enable'
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '11'
            freeCompilerArgs += '-Xjvm-default=enable'
        }
    }
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

project(':base') {
    compileJava {
        options.compilerArgs += '--add-exports=java.base/jdk.internal.misc=vproxy.base'
        options.compilerArgs += ["-h", project.rootProject.projectDir.absolutePath + "/base/src/main/c"]
    }
    compileKotlin {
        destinationDir = compileJava.destinationDir
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.4.31'
        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.4.3'
    }
}

project(':lib') {
    compileKotlin {
        destinationDir = compileJava.destinationDir
    }

    dependencies {
        compile project(':base')
    }
}

project(':core') {
    compileKotlin {
        destinationDir = compileJava.destinationDir
    }

    dependencies {
        compile project(':base')
        compile project(':lib')
    }
}

project(':extended') {
    compileKotlin {
        destinationDir = compileJava.destinationDir
    }

    dependencies {
        compile project(':base')
        compile project(':core')
        compile project(':lib')
    }
}

project(':app') {
    compileKotlin {
        destinationDir = compileJava.destinationDir
    }

    dependencies {
        compile project(':base')
        compile project(':core')
        compile project(':lib')
        compile project(':extended')
    }
}

project(':test') {
    compileTestKotlin {
        destinationDir = compileTestJava.destinationDir
    }

    dependencies {
        compile project(':base')
        compile project(':core')
        compile project(':extended')
        compile project(':lib')
        compile project(':app')

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'io.vertx', name: 'vertx-redis-client', version: '3.7.0'
        testCompile group: 'io.vertx', name: 'vertx-web-client', version: '3.7.0'
        testCompile group: 'io.grpc', name: 'grpc-all', version: '1.20.0'
        testCompile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
        testCompile group: 'org.apache.thrift', name: 'libthrift', version: '0.11.0'
        testCompile group: 'com.alibaba', name: 'dubbo', version: '2.6.6'
    }

    task runSuite(type: Test) {
        jvmArgs += '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'

        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED
            maxGranularity 100
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }

        include '**/VSuite.class'
    }

    task runCI(type: Test) {
        jvmArgs += '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'

        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED
            maxGranularity 100
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }

        include '**/CI.class'
    }

    task runTest {
        dependsOn clean
        dependsOn runSuite
        dependsOn runCI
    }

    task runSingleTest(type: Test) {
        outputs.upToDateWhen { return false }

        doFirst {
            def c = System.getProperty("case")
            if (c == null) {
                def message = "missing test case to run, specify it with -Dcase"
                println(message)
                throw new Exception(message)
            }

            jvmArgs += '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'

            testLogging {
                events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED
                maxGranularity 100
                exceptionFormat "full"
                showCauses true
                showExceptions true
                showStackTraces true
            }

            include '**/' + c + '.class'
        }
    }

    task XDPPoc(type: JavaExec) {
        if ("true" == System.getProperty("enableAssertions")) {
            enableAssertions = true
        }
        classpath = sourceSets.test.runtimeClasspath
        workingDir = project.rootProject.rootDir.getAbsolutePath()
        systemProperty("java.library.path", "base/src/main/c:base/src/main/c/xdp/libbpf/src")
        systemProperty("vfd", "posix")
        main = "vproxy.poc.XDPPoc"
        if (System.getProperty("XDPPoc") != null) {
            main += System.getProperty("XDPPoc")
        }

        if (System.getProperty("ifname") != null) {
            args = [System.getProperty("ifname")]
        }
    }
}

dependencies {
    compile project(':app')
    testCompile project(':test')
}

def loadVersion() {
    def PREFIX = "public static final String VERSION = \""
    def SUFFIX = "\"; // _THE_VERSION_"
    def ver = file(projectDir.getAbsolutePath() + "/base/src/main/java/vproxy/base/util/Version.java")
    def lines = ver.getText().split("\n")
    for (def line : lines) {
        line = line.trim()
        if (line.startsWith(PREFIX) && line.endsWith(SUFFIX)) {
            return line.substring(PREFIX.length(), line.length() - SUFFIX.length())
        }
    }
    return "unknown"
}
